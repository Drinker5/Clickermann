$g_wait = 200
$key_wait = 20
$addnalog_x = $_xmouse
$addnalog_y = $_ymouse
$nalog_vichet_combo_x = 170
$nalog_vichet_combo_y = -670
$auto_curs_btn_x = 130
$auto_curs_btn_y = -380
$summa_dohoda_input_x = 170
$summa_dohoda_input_y = -595
SUB(ALTTAB) 
  KEYDOWN (#ALT)
  WAITMS ($key_wait)
  KEYDOWN (#TAB)
  WAITMS ($key_wait)
  KEYUP (#TAB)
  WAITMS ($key_wait)
  KEYUP (#ALT)
  WAITMS($g_wait)
END_SUB

SUB(COPY) 
	KEYDOWN (#CTRL)
	WAITMS ($key_wait)
	KEYDOWN (#C)
	WAITMS ($key_wait)
	KEYUP (#C)
	KEYUP (#CTRL)
	WAITMS($g_wait)
END_SUB

SUB(PASTE) 
	KEYDOWN (#CTRL)
	WAITMS ($key_wait)
	KEYDOWN (#V)
	WAITMS ($key_wait)
	KEYUP (#V)
	KEYUP (#CTRL)
	WAITMS($g_wait)
END_SUB

SUB(TAB, $repeats) 
	FOR($i=0,$i<$repeats)
		KEYPRESS(#TAB)
		WAITMS($key_wait)
	END_CYC
	WAITMS($g_wait)
END_SUB

SUB(PRESS, $KEY, $repeats) 
	FOR($i=0,$i<$repeats)
		KEYPRESS($KEY)
		WAITMS($key_wait)
	END_CYC
	WAITMS($g_wait)
END_SUB

SUB(SHIFTTAB)
  KEYDOWN(#SHIFT)
  WAITMS ($key_wait)
	TAB(1)
	KEYUP(#SHIFT) 
	WAITMS($g_wait)
END_SUB

SUB(ENTER)
	KEYPRESS(#ENTER)
	WAITMS($g_wait)
END_SUB

SUB(SPACE)
	KEYPRESS(#SPACE)
	WAITMS($g_wait)
END_SUB

SUB(ADDNALOG)
	LCLICK($addnalog_x,$addnalog_y)
	WAITMS($key_wait)
	KEYPRESS(#PAGEDOWN)
	WAITMS($g_wait)
END_SUB

SUB(OPENNALOG)
	LCLICK($opennalog_x,$opennalog_y)
	WAITMS($g_wait)
END_SUB

SUB(INPUT_KOD_DOHODA)
  KEYSTRING("1")
  ENTER()
	KEYSTRING("010")
	WAITMS($g_wait)
  KEYPRESS(#DOWN)
	WAITMS($key_wait)
	ENTER()
END_SUB

SUB(INPUT_COUNTRY)
	KEYPRESS(#DOWN)
	WAITMS($key_wait)
	KEYPRESS(#DOWN)
	WAITMS($g_wait)
END_SUB

SUB(INPUT_NALOG_VICHET)
	KEYPRESS(#DOWN)
	WAITMS($key_wait)
	KEYPRESS(#DOWN)
	WAITMS($g_wait)
END_SUB

SUB(EXCEL_COPY_NEXT)
	COPY()
	TAB(1)
	WAITMS($g_wait)
END_SUB


SUB(SELECT_INPUT)
  KEYPRESS(#A)
  WAITMS($g_wait)
  ENTER()
  TAB(1)
END_SUB

SUB(SELECT_FIRST_OPTION)
 	KEYPRESS(#DOWN)
	WAITMS($g_wait) 
  ENTER()
END_SUB

SUB(PASTE_NEXT_FROM_EXCEL)
  ALTTAB()
  EXCEL_COPY_NEXT()
  ALTTAB()
  PASTE()
END_SUB

SUB(NEXT_ROW_EXCEL)
  KEYPRESS(#DOWN)
	WAITMS($g_wait)
	KEYPRESS(#HOME)
  WAITMS($g_wait)	 
END_SUB

SUB(ADD_OBJECT)
  TAB(2)
  PASTE_NEXT_FROM_EXCEL() // Наименование     
  TAB(3)
  SELECT_INPUT()
  PASTE_NEXT_FROM_EXCEL() // Страна
  SELECT_FIRST_OPTION()
  TAB(9)
  SELECT_INPUT()
	PASTE()
	SELECT_FIRST_OPTION()
  TAB(3)
  ENTER()
END_SUB

SUB(SELECT_NALOG_VICHET)
  PRESS(#PAGEDOWN,3)
  WAITMS($g_wait) 
  LCLICK($nalog_vichet_combo_x,$nalog_vichet_combo_y)
  WAITMS($g_wait) 
  PRESS(#DOWN, 2)
	ENTER()
END_SUB

SUB(SELECT_SUMMA_DOHODA)
  PRESS(#PAGEDOWN,3)
  WAITMS($g_wait) 
  LCLICK($summa_dohoda_input_x,$summa_dohoda_input_y)
  WAITMS($g_wait) 
END_SUB

//=============================================

//NALOG.RU
ADDNALOG()
ADD_OBJECT()

TAB(1)
INPUT_KOD_DOHODA()
SELECT_NALOG_VICHET()

// Сумма дохода
SELECT_SUMMA_DOHODA()
PASTE_NEXT_FROM_EXCEL()

// Дата получения дохода
TAB(2)
PASTE_NEXT_FROM_EXCEL()
TAB(1)
PASTE()

// Наименование валюты
TAB(3)
SELECT_INPUT()
SHIFTTAB()
PASTE_NEXT_FROM_EXCEL()
SELECT_FIRST_OPTION()
PRESS(#PAGEDOWN,3)
WAITMS($g_wait) 
LCLICK($auto_curs_btn_x,$auto_curs_btn_y)
WAITMS($g_wait) 
TAB(1)
ALTTAB()
EXCEL_COPY_NEXT()
NEXT_ROW_EXCEL()
ALTTAB()
PASTE()
PRESS(#PAGEUP,5)

MOVE($addnalog_x,$addnalog_y)

HALT